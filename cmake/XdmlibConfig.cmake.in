# Copyright 2016 The XMPMeta Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Config file for xdmlib - Find xdmlib & dependencies.
#
# This file is used by CMake when find_package(Xdmlib) is invoked and either
# the directory containing this file either is present in CMAKE_MODULE_PATH
# (if Xdmlib was installed), or exists in the local CMake package registry if
# the Xdmlib build directory was exported.
#
# This module defines the following variables:
#
# Xdmlib_FOUND / XDMLIB_FOUND: True if Xdmlib has been successfully
#                            found. Both variables are set as although
#                            FindPackage() only references Xdmlib_FOUND
#                            in Config mode, given the conventions for
#                            <package>_FOUND when FindPackage() is
#                            called in Module mode, users could
#                            reasonably expect to use XDMLIB_FOUND
#                            instead.
#
# XDMLIB_VERSION: Version of Xdmlib found.
#
# XDMLIB_INCLUDE_DIRS: Include directories for Xdmlib and the
#                     dependencies which appear in the Xdmlib public
#                     API and are thus required to use Xdmlib.
#
# XDMLIB_LIBRARIES: Libraries for Xdmlib and all
#                  dependencies against which Xdmlib was
#                  compiled. This will not include any optional
#                  dependencies that were disabled when Xdmlib was
#                  compiled.
#
# The following variables are also defined for legacy compatibility
# only.  Any new code should not use them as they do not conform to
# the standard CMake FindPackage naming conventions.
#
# XDMLIB_INCLUDES = ${XDMLIB_INCLUDE_DIRS}.

# Called if we failed to find Xdmlib or any of its required dependencies,
# unsets all public (designed to be used externally) variables and reports
# error message at priority depending upon [REQUIRED/QUIET/<NONE>] argument.
macro(XDMLIB_REPORT_NOT_FOUND REASON_MSG)
  # FindPackage() only references Xdmlib_FOUND, and requires it to be
  # explicitly set FALSE to denote not found (not merely undefined).
  set(Xdmlib_FOUND FALSE)
  set(XDMLIB_FOUND FALSE)
  unset(XDMLIB_INCLUDE_DIRS)
  unset(XDMLIB_LIBRARIES)

  # Reset the CMake module path to its state when this script was called.
  set(CMAKE_MODULE_PATH ${CALLERS_CMAKE_MODULE_PATH})

  # Note <package>_FIND_[REQUIRED/QUIETLY] variables defined by
  # FindPackage() use the camelcase library name, not uppercase.
  if (Xdmlib_FIND_QUIETLY)
    message(STATUS "Failed to find Xdmlib - " ${REASON_MSG} ${ARGN})
  else (Xdmlib_FIND_REQUIRED)
    message(FATAL_ERROR "Failed to find Xdmlib - " ${REASON_MSG} ${ARGN})
  else()
    # Neither QUIETLY nor REQUIRED, use SEND_ERROR which emits an error
    # that prevents generation, but continues configuration.
    message(SEND_ERROR "Failed to find Xdmlib - " ${REASON_MSG} ${ARGN})
  endif ()
  return()
endmacro(XDMLIB_REPORT_NOT_FOUND)

# If Xdmlib was not installed, then by definition it was exported
# from a build directory.
set(XDMLIB_WAS_INSTALLED @SETUP_XDMLIB_CONFIG_FOR_INSTALLATION@)

# Record the state of the CMake module path when this script was
# called so that we can ensure that we leave it in the same state on
# exit as it was on entry, but modify it locally.
set(CALLERS_CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH})

# Get the (current, i.e. installed) directory containing this file.
get_filename_component(XDMLIB_CURRENT_CONFIG_DIR
  "${CMAKE_CURRENT_LIST_FILE}" PATH)

if (XDMLIB_WAS_INSTALLED)
  # Reset CMake module path to the installation directory of this
  # script, thus we will use the FindPackage() scripts shipped with
  # Xdmlib to find Xdmlib' dependencies, even if the user has equivalently
  # named FindPackage() scripts in their project.
  set(CMAKE_MODULE_PATH ${XDMLIB_CURRENT_CONFIG_DIR})

  # Build the absolute root install directory as a relative path
  # (determined when Xdmlib was configured & built) from the current
  # install directory for this this file.  This allows for the install
  # tree to be relocated, after Xdmlib was built, outside of CMake.
  get_filename_component(CURRENT_ROOT_INSTALL_DIR
    ${XDMLIB_CURRENT_CONFIG_DIR}/@INSTALL_ROOT_REL_CONFIG_INSTALL_DIR@
    ABSOLUTE)
  if (NOT EXISTS ${CURRENT_ROOT_INSTALL_DIR})
    xdmlib_report_not_found(
      "Xdmlib install root: ${CURRENT_ROOT_INSTALL_DIR}, "
      "determined from relative path from XdmlibConfig.cmake install location: "
      "${XDMLIB_CURRENT_CONFIG_DIR}, does not exist. Either the install "
      "directory was deleted, or the install tree was only partially relocated "
      "outside of CMake after Xdmlib was built.")
  endif (NOT EXISTS ${CURRENT_ROOT_INSTALL_DIR})

  # Set the include directories for Xdmlib (itself).
  set(XDMLIB_INCLUDE_DIR "${CURRENT_ROOT_INSTALL_DIR}/includes")
  if (NOT EXISTS ${XDMLIB_INCLUDE_DIR}/xdmlib/xdmlib.h)
    xdmlib_report_not_found(
      "Xdmlib install root: ${CURRENT_ROOT_INSTALL_DIR}, "
      "determined from relative path from XdmlibConfig.cmake install location: "
      "${XDMLIB_CURRENT_CONFIG_DIR}, does not contain Xdmlib headers. "
      "Either the install directory was deleted, or the install tree was only "
      "partially relocated outside of CMake after Xdmlib was built.")
  endif (NOT EXISTS ${XDMLIB_INCLUDE_DIR}/xdmlib/xdmlib.h)
  list(APPEND XDMLIB_INCLUDE_DIRS ${XDMLIB_INCLUDE_DIR})

else(XDMLIB_WAS_INSTALLED)
  # Xdmlib was exported from the build tree.
  set(XDMLIB_EXPORTED_BUILD_DIR ${XDMLIB_CURRENT_CONFIG_DIR})
  get_filename_component(XDMLIB_EXPORTED_SOURCE_DIR
    ${XDMLIB_EXPORTED_BUILD_DIR}/@INSTALL_ROOT_REL_CONFIG_INSTALL_DIR@
    ABSOLUTE)
  if (NOT EXISTS ${XDMLIB_EXPORTED_SOURCE_DIR})
    xdmlib_report_not_found(
      "Xdmlib exported source directory: ${XDMLIB_EXPORTED_SOURCE_DIR}, "
      "determined from relative path from XdmlibConfig.cmake exported build "
      "directory: ${XDMLIB_EXPORTED_BUILD_DIR} does not exist.")
  endif()

  # Reset CMake module path to the cmake directory in the Xdmlib source
  # tree which was exported, thus we will use the FindPackage() scripts shipped
  # with Xdmlib to find Xdmlib' dependencies, even if the user has equivalently
  # named FindPackage() scripts in their project.
  set(CMAKE_MODULE_PATH ${XDMLIB_EXPORTED_SOURCE_DIR}/cmake)

  # Set the include directories for Xdmlib (itself).
  set(XDMLIB_INCLUDE_DIR "${XDMLIB_EXPORTED_SOURCE_DIR}/include")
  if (NOT EXISTS ${XDMLIB_INCLUDE_DIR}/xdmlib/xdmlib.h)
    xdmlib_report_not_found(
      "Xdmlib exported source directory: ${XDMLIB_EXPORTED_SOURCE_DIR}, "
      "determined from relative path from XdmlibConfig.cmake exported build "
      "directory: ${XDMLIB_EXPORTED_BUILD_DIR}, does not contain Xdmlib headers.")
  endif (NOT EXISTS ${XDMLIB_INCLUDE_DIR}/xdmlib/xdmlib.h)
  list(APPEND XDMLIB_INCLUDE_DIRS ${XDMLIB_INCLUDE_DIR})

  # Append the path to the configured config.h in the exported build directory
  # to the Xdmlib include directories.
  set(XDMLIB_CONFIG_FILE
    ${XDMLIB_EXPORTED_BUILD_DIR}/config/xdmlib/internal/config.h)
  if (NOT EXISTS ${XDMLIB_CONFIG_FILE})
    xdmlib_report_not_found(
      "Xdmlib exported build directory: ${XDMLIB_EXPORTED_BUILD_DIR}, "
      "does not contain required configured Xdmlib config.h, it is not here: "
      "${XDMLIB_CONFIG_FILE}.")
  endif (NOT EXISTS ${XDMLIB_CONFIG_FILE})
  list(APPEND XDMLIB_INCLUDE_DIRS ${XDMLIB_EXPORTED_BUILD_DIR}/config)
endif(XDMLIB_WAS_INSTALLED)

# Set the version.
set(XDMLIB_VERSION @XDMLIB_VERSION@ )

# libxml
# Flag set during configuration and build of Xdmlib.
set(XDMLIB_EIGEN_VERSION @EIGEN_VERSION@)
# Append the locations of libxml when Xdmlib was built to the search path hints.
list(APPEND EIGEN_INCLUDE_DIR_HINTS @EIGEN_INCLUDE_DIR@)
# Search quietly to control the timing of the error message if not found. The
# search should be for an exact match, but for usability reasons do a soft
# match and reject with an explanation below.
find_package(libxml ${XDMLIB_LIBXML_VERSION} QUIET)
if (LIBXML_FOUND)
  if (NOT LIBXML_VERSION VERSION_EQUAL XDMLIB_EIGEN_VERSION)
    # CMake's VERSION check in FIND_PACKAGE() will accept any version >= the
    # specified version. However, only version = is supported. Improve
    # usability by explaining why we don't accept non-exact version matching.
    xdmlib_report_not_found("Found libxml dependency, but the version of libxml "
      "found (${LIBXML_VERSION}) does not exactly match the version of libxml "
      "Xdmlib was compiled with (${XDMLIB_LIBXML_VERSION}), and may not work "
      "property with ${LIBXML_VERSION}")
  endif ()
  message(STATUS "Found required Xdmlib dependency: "
    "libxml version ${XDMLIB_LIBXML_VERSION} in ${LIBXML_INCLUDE_DIRS}")
else (LIBXML_FOUND)
  xdmlib_report_not_found("Missing required Xdmlib "
    "dependency: libxml version ${XDMLIB_LIBXML_VERSION}, please set "
    "LIBXML_INCLUDE_DIR.")
endif (LIBXML_FOUND)
list(APPEND XDMLIB_INCLUDE_DIRS ${LIBXML_INCLUDE_DIRS})

# Glog.
# Flag set during configuration and build of Xdmlib.
set(XDMLIB_USES_MINIGLOG @MINIGLOG@)
if (XDMLIB_USES_MINIGLOG)
  set(MINIGLOG_INCLUDE_DIR ${XDMLIB_INCLUDE_DIR}/xdmlib/internal/miniglog)
  if (NOT EXISTS ${MINIGLOG_INCLUDE_DIR})
    xdmlib_report_not_found(
      "Xdmlib include directory: "
      "${XDMLIB_INCLUDE_DIR} does not include miniglog, but Xdmlib was "
      "compiled with MINIGLOG enabled (in place of Glog).")
  endif (NOT EXISTS ${MINIGLOG_INCLUDE_DIR})
  list(APPEND XDMLIB_INCLUDE_DIRS ${MINIGLOG_INCLUDE_DIR})
  # Output message at standard log level (not the lower STATUS) so that
  # the message is output in GUI during configuration to warn user.
  message("-- Found Xdmlib compiled with miniglog substitute "
    "for glog, beware this will likely cause problems if glog is later linked.")
else (XDMLIB_USES_MINIGLOG)
  # Append the locations of glog when Xdmlib was built to the search path hints.
  list(APPEND GLOG_INCLUDE_DIR_HINTS @GLOG_INCLUDE_DIR@)
  get_filename_component(XDMLIB_BUILD_GLOG_LIBRARY_DIR @GLOG_LIBRARY@ PATH)
  list(APPEND GLOG_LIBRARY_DIR_HINTS ${XDMLIB_BUILD_GLOG_LIBRARY_DIR})

  # Search quietly s/t we control the timing of the error message if not found.
  find_package(Glog QUIET)
  if (GLOG_FOUND)
    message(STATUS "Found required Xdmlib dependency: "
      "Glog in ${GLOG_INCLUDE_DIRS}")
  else (GLOG_FOUND)
    xdmlib_report_not_found("Missing required Xdmlib "
      "dependency: Glog, please set GLOG_INCLUDE_DIR.")
  endif (GLOG_FOUND)
  list(APPEND XDMLIB_INCLUDE_DIRS ${GLOG_INCLUDE_DIRS})
endif (XDMLIB_USES_MINIGLOG)

# Import exported Xdmlib targets, if they have not already been imported.
if (NOT TARGET xdmlib AND NOT Xdmlib_BINARY_DIR)
  include(${XDMLIB_CURRENT_CONFIG_DIR}/XdmlibTargets.cmake)
endif (NOT TARGET xdmlib AND NOT Xdmlib_BINARY_DIR)
# Set the expected XX_LIBRARIES variable for FindPackage().
set(XDMLIB_LIBRARIES xdmlib)

# Make user aware of any compile flags that will be added to their targets
# which use Xdmlib (i.e. flags exported in the Xdmlib target).  Only CMake
# versions >= 2.8.12 support target_compile_options().
if (TARGET ${XDMLIB_LIBRARIES} AND
    NOT CMAKE_VERSION VERSION_LESS "2.8.12")
  get_target_property(XDMLIB_INTERFACE_COMPILE_OPTIONS
    ${XDMLIB_LIBRARIES} INTERFACE_COMPILE_OPTIONS)

  if (XDMLIB_WAS_INSTALLED)
    set(XDMLIB_LOCATION "${CURRENT_ROOT_INSTALL_DIR}")
  else()
    set(XDMLIB_LOCATION "${XDMLIB_EXPORTED_BUILD_DIR}")
  endif()

  # Check for -std=c++11 flags.
  if (XDMLIB_INTERFACE_COMPILE_OPTIONS MATCHES ".*std=c\\+\\+11.*")
    message(STATUS "Xdmlib version ${XDMLIB_VERSION} detected here: "
      "${XDMLIB_LOCATION} was built with C++11. Xdmlib target will add "
      "C++11 flags to compile options for targets using it.")
  endif()
endif()

# Set legacy include directories variable for backwards compatibility.
set(XDMLIB_INCLUDES ${XDMLIB_INCLUDE_DIRS})

# Reset CMake module path to its state when this script was called.
set(CMAKE_MODULE_PATH ${CALLERS_CMAKE_MODULE_PATH})

# As we use XDMLIB_REPORT_NOT_FOUND() to abort, if we reach this point we have
# found Xdmlib and all required dependencies.
if (XDMLIB_WAS_INSTALLED)
  message(STATUS "Found Xdmlib version: ${XDMLIB_VERSION} "
    "installed in: ${CURRENT_ROOT_INSTALL_DIR}")
else (XDMLIB_WAS_INSTALLED)
  message(STATUS "Found Xdmlib version: ${XDMLIB_VERSION} "
    "exported from build directory: ${XDMLIB_EXPORTED_BUILD_DIR}")
endif()

# Set XDMLIB_FOUND to be equivalent to Xdmlib_FOUND, which is set to
# TRUE by FindPackage() if this file is found and run, and after which
# Xdmlib_FOUND is not (explicitly, i.e. undefined does not count) set
# to FALSE.
set(XDMLIB_FOUND TRUE)
